apply plugin: 'java'
apply plugin: 'antlr'

project.ext.set('libsDir', 'lib')

repositories {
    mavenCentral()
    flatDir {
        dirs project.libsDir
    }
}

sourceSets {
    generated {
        java {
            srcDir 'src/generated/java'
        }
        compileClasspath += configurations.compile
        runtimeClasspath += configurations.runtime
    }

    main {
        compileClasspath += sourceSets.generated.output
        runtimeClasspath += sourceSets.generated.output
    }

    test {
        compileClasspath += sourceSets.generated.output
        runtimeClasspath += sourceSets.generated.output
    }
}

dependencies {
    compile name: 'com.microsoft.z3'
    runtime group: 'org.antlr', name: 'antlr4-runtime', version: '4.5'
    antlr group: 'org.antlr', name: 'antlr4', version: '4.5'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

generateGrammarSource {
    outputDirectory = new File("${project.projectDir}/src/generated/java/Parser")
}

test {
    useJUnit {
        excludeCategories 'Categories.Benchmark','Categories.Regression'
    }
    testLogging.showStandardStreams = true
    systemProperty 'java.library.path', project.libsDir
}

task benchmark(type: Test) {
    useJUnit {
        includeCategories 'Categories.Benchmark'
    }
}
