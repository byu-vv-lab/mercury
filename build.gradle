apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'idea'

project.ext.set('libsDir', 'lib')

idea {
    module {
        excludeDirs = [file(".gradle")]
        ["classes", "docs", "dependency-cache", "libs", "reports", "resources", "test-results", "tmp"].each {
            excludeDirs << file("$buildDir/$it")
        }
    }
}

repositories {
    flatDir {
        dirs project.libsDir
    }
    mavenCentral()
}

dependencies {
    compile name: 'com.microsoft.z3'
    runtime group: 'org.antlr', name: 'antlr4-runtime', version: '4.5'
    antlr group: 'org.antlr', name: 'antlr4', version: '4.5'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
    useJUnit {
        excludeCategories 'Categories.Benchmark','Categories.Regression'
    }
    systemProperty 'java.library.path', project.libsDir
}

task benchmark(type: Test) {
    useJUnit {
        includeCategories 'Categories.Benchmark'
    }
    systemProperty 'java.library.path', project.libsDir
}

task regression(type: Test) {
    useJUnit {
        includeCategories 'Categories.Regression'
    }
    systemProperty 'java.library.path', project.libsDir
}

task testAll(type: Test) {
    useJUnit {}
    systemProperty 'java.library.path', project.libsDir
}
